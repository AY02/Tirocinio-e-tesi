theory CloneDetectionResponseVerify
begin

builtins: signing, natural-numbers

// Limite per i contatori perche' altrimenti si hanno loop infiniti sulle response.
restriction CounterUpperBound:
  "All n m #i.
  LessThan(n, m) @i
  ==>
  m << %1 %+ %1 %+ %1 %+ %1"

// Un client genera una coppia di chiavi e si registra presso un server.
// Inizializzazione dei contatori.
rule Setup:
    [ Fr(~sk) ]
  --[ ClientRegisters($c, $s, ~sk, pk(~sk)) ]->
    [ !ClientSK($c, $s, ~sk),
      !ServerPK($s, $c, pk(~sk)),
      ClientC($c, $s, %1),
      ServerC($s, $c, %1),
      Out(pk(~sk)) ]

// Ogni client si puo' registrare ad uno stesso server al piu' una volta sola.
// Ogni client genera una coppia di chiavi per quello stesso server al piu' una volta sola.
restriction ClientRegistersOnce:
  "All c s skey1 skey2 pkey1 pkey2 #i #j.
    ClientRegisters(c, s, skey1, pkey1) @i &
    ClientRegisters(c, s, skey2, pkey2) @j
    ==>
    #i = #j"

// Rivela la chiave segreta di un client (equivale alla clonazione).
// Effettua un'autenticazione riuscita dell'attaccante.
// Il contatore del server viene incrementato, ma quello del client resta invariato.
rule RevealSK:
    [ !ClientSK(c, s, skey),
      ClientC(c, s, %n_c),
      ServerC(s, c, %n_s) ]
  --[ Reveal(c, s) ]->
    [ ClientC(c, s, %n_c),
      ServerC(s, c, %n_c %+ %1),
      Out(skey) ]

// La reveal della chiave segreta di un client puo' essere fatta una sola volta.
restriction RevealOnce:
  "All c s #i #j.
    Reveal(c, s) @i &
    Reveal(c, s) @j
    ==>
    #i = #j"

// Il server invia una challenge al client.
rule Challenge:
    [ !ServerPK(s, c, pkey),
      Fr(~ch) ]
  --[ ServerSendsCH(s, c, ~ch) ]->
    [ ServerWaitingSIG(s, c, ~ch),
      Out(<'Challenge', s, c, ~ch>) ]

// Il client riceve la challenge, la firma, incrementa il suo contatore e invia (firma, contatore) al server.
rule Response:
  let %next_n = %n %+ %1
  in
    [ In(<'Challenge', s, c, ch>),
      ClientC(c, s, %n),
      !ClientSK(c, s, skey) ]
  --[ ResponseUsed(c, s),
      ClientResponds(c, s, ch) ]->
    [ ClientC(c, s, %next_n),
      Out(<'Response', c, s, ch, %next_n, sign(<ch, %next_n>, skey)>) ]

// Dopo la reveal, il client e l'attaccante non possono piu' usare Response.
restriction NoResponseAfterReveal:
  "not (Ex c s #i1 #i2.
    Reveal(c, s) @i1 &
    ResponseUsed(c, s) @i2 &
    #i1 < #i2)"

// Il server riceve la firma, confronta i contatori e verifica che la firma sia corretta.
rule Verify:
    [ In(<'Response', c, s, ch, %n_c, sig>),
      ServerWaitingSIG(s, c, ch),
      ServerC(s, c, %n_s),
      !ServerPK(s, c, pkey) ]
  --[ VerifyUsed(c, s),
      LessThan(%n_s, %n_c),
      Eq(verify(sig, <ch, %n_c>, pkey), true),
      ServerAccepts(s, c, ch) ]->
    [ ServerC(s, c, %n_c) ]

// Dopo la reveal, il server non puo' piu' fare la Verify.
restriction NoVerifyAfterReveal:
  "not (Ex c s #i1 #i2.
    Reveal(c, s) @i1 &
    VerifyUsed(c, s) @i2 &
    #i1 < #i2)"

// Diamo la possibilita' al client di autenticarsi anche dopo essere stato clonato.
// Impediamo all'attaccante di fare il riallineamento (il client non verra' mai piu' autenticato).
rule ResponseVerify:
  let %next_n_c = %n_c %+ %1
      sig = sign(<ch, %next_n_c>, skey)
  in
    [ In(<'Challenge', s, c, ch>),
      ServerWaitingSIG(s, c, ch),
      ClientC(c, s, %n_c),
      ServerC(s, c, %n_s),
      !ClientSK(c, s, skey),
      !ServerPK(s, c, pkey) ]
  --[ ResponseVerifyUsed(c, s),
      ClientResponds(c, s, ch),
      LessThan(%n_s, %next_n_c),
      Eq(verify(sig, <ch, %next_n_c>, pkey), true),
      ServerAccepts(s, c, ch) ]->
    [ ClientC(c, s, %next_n_c),
      ServerC(s, c, %next_n_c) ]

// Prima della reveal, non si puo' usare ResponseVerify.
restriction OnlyResponseVerifyAfterReveal:
  "not (Ex c s #i2.
    ResponseVerifyUsed(c, s) @i2 &
    not (Ex #i1. Reveal(c, s) @i1 & #i1 < #i2))"

// Ogni volta che viene usata l'equazione Eq(x, y), x deve essere uguale a y.
restriction Equality:
  "All x y #i.
    Eq(x, y) @i
    ==>
    x = y"

// Ogni volta che viene usata la disequazione LessThan(x, y), x deve essere minore di y.
restriction LessThan:
  "All x y #i.
    LessThan(x, y) @i
    ==>
    x << y"

// -----------------------------LEMMI DA DIMOSTRARE-----------------------------

// Se il server ha accettato un client e non e' stato clonato, allora quest'ultimo deve aver risposto in precedenza.
lemma ServerAcceptsOnlyIfClientResponded:
  "All c s ch #i3.
    ServerAccepts(s, c, ch) @i3 &
    not (Ex #i1. Reveal(c, s) @i1 & #i1 < #i3)
    ==>
    (Ex #i2. ClientResponds(c, s, ch) @i2 & #i2 < #i3)"

// Se il client e' stato clonato, allora non puo' piu' autenticarsi.
// Vero: L'attaccante non puo' piu' riallineare il contatore del client.
lemma ClonedClientCantAuthenticate:
  "All c s #i1.
    Reveal(c, s) @i1
    ==>
    not (Ex #i2. ResponseVerifyUsed(c, s) @i2 & #i1 < #i2)"

// Riallineamento banale del contatore del client.
// Non esiste perche' l'attaccante non puo' piu' riallineare.
lemma ClientRealignmentExists:
  exists-trace
  "Ex c s skey pkey ch f_ch #i1 #i2 #i3 #i4 #i5 #i6.
    ClientRegisters(c, s, skey, pkey) @i1 &
    Reveal(c, s) @i2 &
    ClientResponds(c, s, f_ch) @i3 &
    ServerSendsCH(s, c, ch) @i4 &
    ClientResponds(c, s, ch) @i5 &
    ServerAccepts(s, c, ch) @i6 &
    #i1 < #i2 & #i2 < #i3 & #i3 < #i4 & #i4 < #i5 & #i5 < #i6 &
    // Limita eventuali altre response invocate dall'attaccante
    (All c s ch #i.
      ClientResponds(c, s, ch) @i
      ==>
      (#i = #i3 | #i = #i5))"

end