theory CloneDetection
begin

builtins: signing, natural-numbers

// Un client genera una coppia di chiavi e si registra presso un server.
// Inizializzazione dei contatori.
rule Setup:
    [ Fr(~sk) ]
  --[ ClientRegisters($c, $s, ~sk, pk(~sk)) ]->
    [ !ClientSK($c, $s, ~sk),
      !ServerPK($s, $c, pk(~sk)),
      ClientC($c, $s, %1),
      ServerC($s, $c, %1),
      Out(pk(~sk)) ]

// Ogni client si puo' registrare ad uno stesso server al piu' una volta sola.
// Ogni client genera una coppia di chiavi per quello stesso server al piu' una volta sola.
restriction ClientRegistersOnce:
  "All c s skey1 skey2 pkey1 pkey2 #i #j.
    ClientRegisters(c, s, skey1, pkey1) @i &
    ClientRegisters(c, s, skey2, pkey2) @j
    ==>
    #i = #j"

// Il server invia una challenge al client.
rule Challenge:
    [ !ServerPK(s, c, pkey),
      Fr(~ch) ]
  --[ ServerSendsCH(s, c, ~ch) ]->
    [ ServerWaitingSIG(s, c, ~ch),
      Out(<'Challenge', s, c, ~ch>) ]

// Il client riceve la challenge, la firma, incrementa il suo contatore e invia (firma, contatore) al server.
rule Response:
  let %next_n = %n %+ %1
  in
    [ In(<'Challenge', s, c, ch>),
      ClientC(c, s, %n),
      !ClientSK(c, s, skey) ]
  --[ ClientResponds(c, s, ch, sign(ch, skey)) ]->
    [ ClientC(c, s, %next_n),
      Out(<'Response', c, s, ch, sign(ch, skey), %next_n>) ]

// Il server riceve la firma, confronta i contatori e verifica che la firma sia corretta.
rule Verify:
    [ In(<'Response', c, s, ch, sig, %n_c>),
      ServerWaitingSIG(s, c, ch),
      ServerC(s, c, %n_s),
      !ServerPK(s, c, pkey) ]
  --[ LessThan(%n_s, %n_c),
      Eq(verify(sig, ch, pkey), true),
      ServerAccepts(s, c, ch, sig) ]->
    [ ServerC(s, c, %n_c) ]

// Ogni volta che viene usata l'equazione Eq(x, y), x deve essere uguale a y.
restriction Equality:
  "All x y #i.
    Eq(x, y) @i
    ==>
    x = y"

// Ogni volta che viene usata la disequazione LessThan(x, y), x deve essere minore di y.
restriction LessThan:
  "All x y #i.
    LessThan(x, y) @i
    ==>
    x << y"

// Rivela una chiave segreta di un client (equivale alla clonazione).
rule RevealSK:
    [ !ClientSK(c, s, skey) ]
  --[ Reveal(c, s) ]->
    [ Out(skey) ]

// -----------------------------LEMMI DA DIMOSTRARE-----------------------------

// Se il server ha accettato un client, allora quest'ultimo deve aver risposto in precedenza.
lemma ServerAcceptsOnlyIfClientResponded:
  "All c s ch sig #i.
    ServerAccepts(s, c, ch, sig) @i &
    not (Ex #z. Reveal(c, s) @z & #z < #i)
    ==>
    (Ex #j. ClientResponds(c, s, ch, sig) @j & #j < #i)"

// Se il client e' stato clonato, allora non riesce piu' ad autenticarsi.
// Falso: L'attaccante riallinea il contatore del client.
lemma ClonedClientCantAuthenticate:
  "All c s ch sig #i.
    ClientResponds(c, s, ch, sig) @i &
    (Ex #z. Reveal(c, s) @z & #z < #i)
    ==>
    not (Ex #j. ServerAccepts(s, c, ch, sig) @j & #i < #j)"

end