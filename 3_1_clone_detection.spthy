theory CloneDetection
begin

builtins: signing, natural-numbers

// Un client genera una coppia di chiavi e si registra presso un server.
// Inizializzazione dei contatori.
rule Setup:
    [ Fr(~sk) ]
  --[ ClientRegisters($c, $s, ~sk, pk(~sk)) ]->
    [ !ClientSK($c, $s, ~sk),
      !ServerPK($s, $c, pk(~sk)),
      ClientC($c, $s, %1),
      ServerC($s, $c, %1),
      Out(pk(~sk)) ]

// Ogni client si puo' registrare ad uno stesso server al piu' una volta sola.
// Ogni client genera una coppia di chiavi per quello stesso server al piu' una volta sola.
restriction ClientRegistersOnce:
  "All c s skey1 skey2 pkey1 pkey2 #i #j.
    ClientRegisters(c, s, skey1, pkey1) @i &
    ClientRegisters(c, s, skey2, pkey2) @j
    ==>
    #i = #j"

// Rivela la chiave segreta di un client (equivale alla clonazione).
// Effettua un'autenticazione riuscita dell'attaccante.
// Il contatore del server viene incrementato, ma quello del client resta invariato.
rule RevealSK:
    [ !ClientSK(c, s, skey),
      ClientC(c, s, %n_c),
      ServerC(s, c, %n_s) ]
  --[ Reveal(c, s) ]->
    [ ClientC(c, s, %n_c),
      ServerC(s, c, %n_c %+ %1),
      Out(skey) ]

// La reveal della chiave segreta di un client puo' essere fatta una sola volta.
restriction RevealOnce:
  "All c s #i #j.
    Reveal(c, s) @i &
    Reveal(c, s) @j
    ==>
    #i = #j"

// Il server invia una challenge al client.
rule Challenge:
    [ !ServerPK(s, c, pkey),
      Fr(~ch) ]
  --[ ServerSendsCH(s, c, ~ch) ]->
    [ ServerWaitingSIG(s, c, ~ch),
      Out(<'Challenge', s, c, ~ch>) ]

// Il client riceve la challenge, la firma, incrementa il suo contatore e invia (firma, contatore) al server.
rule Response:
  let %next_n = %n %+ %1
  in
    [ In(<'Challenge', s, c, ch>),
      ClientC(c, s, %n),
      !ClientSK(c, s, skey) ]
  --[ ClientResponds(c, s, ch, sign(ch, skey), %next_n) ]->
    [ ClientC(c, s, %next_n),
      Out(<'Response', c, s, ch, sign(ch, skey), %next_n>) ]

// Il server riceve la firma, confronta i contatori e verifica che la firma sia corretta.
rule Verify:
    [ In(<'Response', c, s, ch, sig, %n_c>),
      ServerWaitingSIG(s, c, ch),
      ServerC(s, c, %n_s),
      !ServerPK(s, c, pkey) ]
  --[ LessThan(%n_s, %n_c),
      Eq(verify(sig, ch, pkey), true),
      ServerAccepts(s, c, ch, sig, %n_c) ]->
    [ ServerC(s, c, %n_c) ]

// Ogni volta che viene usata l'equazione Eq(x, y), x deve essere uguale a y.
restriction Equality:
  "All x y #i.
    Eq(x, y) @i
    ==>
    x = y"

// Ogni volta che viene usata la disequazione LessThan(x, y), x deve essere minore di y.
restriction LessThan:
  "All x y #i.
    LessThan(x, y) @i
    ==>
    x << y"

// -----------------------------LEMMI DA DIMOSTRARE-----------------------------

// Se il server ha accettato un client, allora quest'ultimo deve aver risposto in precedenza.
// I contatori sono differenziati perche' l'attaccante puo' alterare il contatore del client.
lemma ServerAcceptsOnlyIfClientResponded:
  "All c s ch sig n #i2.
    ServerAccepts(s, c, ch, sig, n) @i2 &
    not (Ex #i. Reveal(c, s) @i)
    ==>
    (Ex m #i1. ClientResponds(c, s, ch, sig, m) @i1 & #i1 < #i2)"

// Se il client e' stato clonato, allora non riesce piu' ad autenticarsi.
// Falso: L'attaccante riallinea il contatore del client.
lemma ClonedClientCantAuthenticate:
  "All c s ch sig n #i1 #i2.
    Reveal(c, s) @i1 &
    ClientResponds(c, s, ch, sig, n) @i2 &
    #i1 < #i2
    ==>
    not (Ex #i3. ServerAccepts(s, c, ch, sig, n) @i3 & #i2 < #i3)"

// Riallineamento banale del contatore del client.
lemma ClientRealignmentExists:
  exists-trace
  "Ex c s skey pkey ch sig f_ch f_sig n2 n3 #i1 #i2 #i3 #i4 #i5 #i6.
    ClientRegisters(c, s, skey, pkey) @i1 &
    Reveal(c, s) @i2 &
    ClientResponds(c, s, f_ch, f_sig, n2) @i3 &
    ServerSendsCH(s, c, ch) @i4 &
    ClientResponds(c, s, ch, sig, n3) @i5 &
    ServerAccepts(s, c, ch, sig, n3) @i6 &
    #i1 < #i2 & #i2 < #i3 & #i3 < #i4 & #i4 < #i5 & #i5 < #i6 &
    (All c s ch sig n #i.
      ClientResponds(c, s, ch, sig, n) @i
      ==>
      (#i = #i3 | #i = #i5))"

end